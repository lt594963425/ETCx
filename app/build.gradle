apply plugin: 'com.android.application'
android {

    compileSdkVersion rootProject.ext.cfg.compileSdkVersion
    buildToolsVersion rootProject.ext.cfg.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.cfg.applicationId
        minSdkVersion rootProject.ext.cfg.minSdkVersion
        targetSdkVersion rootProject.ext.cfg.targetSdkVersion
        versionCode rootProject.ext.pkg.versionCode
        versionName rootProject.ext.pkg.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        jackOptions {
            enabled true
        }
        multiDexEnabled true
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "default_channel"]
    }
    signingConfigs {
        debug {
            storeFile file('C:/ETCXc.keystore')
            storePassword "xcdeetc"
            keyAlias "ETCXcKey"
            keyPassword "xcdeetc"
        }
        release {
            storeFile file('C:/ETCXc.keystore')
            storePassword "xcdeetc"
            keyAlias "ETCXcKey"
            keyPassword "xcdeetc"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix ".${releaseTime()}.debug"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.debug
        }

        release {
            versionNameSuffix ".${releaseTime()}.release"
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "ETCXc_v${defaultConfig.versionName}_${releaseTime()}_${variant.buildType.name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    productFlavors {
        default_channel {}
        wandoujia {}
        xiaomi {}
        tencent {}
        taobao {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    compileOptions {
        //使用JAVA8语法解析
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
//平时打包的文件名包含的时间，只有年月日
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    //okhttp与日志拦截器
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    //rx
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //结合组件生命周期管理Rx
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    //gson
    compile 'com.google.code.gson:gson:2.8.1'
    //图片裁剪库
    compile 'com.github.yalantis:ucrop:2.2.1'
    //高清大图
    compile 'com.shizhefei:LargeImageView:1.0.4'
    //eventBus
    compile 'org.greenrobot:eventbus:3.0.0'
    //轮播图+glide
    compile 'com.youth.banner:banner:1.4.9'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //微信
    compile 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.3.4'
    //友盟
    compile 'com.umeng.analytics:analytics:latest.integration'
}
